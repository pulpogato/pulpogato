import codegen.Main
import de.undercouch.gradle.tasks.download.Download

plugins {
    alias(libs.plugins.javaLibrary)
    alias(libs.plugins.waenaPublished)
}

dependencies {
    compileOnly(libs.annotations)
    compileOnly(libs.springWeb)
    compileOnly(libs.lombok)
    annotationProcessor(libs.lombok)

    api(libs.retrofit)
    api project(":javacat-common")
}


private String getUrl(String projectVariant) {
    def path = projectVariant == 'fpt' ? 'api.github.com' : projectVariant
    return "https://github.com/github/rest-api-description/raw/main/descriptions-next/${path}/${path}.json"
}

var projectVariant = project.name.replace("javacat-rest-", "")

def downloadSchema = tasks.register("downloadSchema", Download) {
    src getUrl(projectVariant)
    dest "${project.layout.buildDirectory.get()}/generated/resources/main/schema.json"
    onlyIfModified true
    tempAndMove true
    useETag true

    inputs.property("url", getUrl(projectVariant))
    outputs.file("${project.layout.buildDirectory.get()}/generated/resources/main/schema.json")
}

def generateJava = tasks.register("generateJava") {
    dependsOn(downloadSchema)
    inputs.file("${project.layout.buildDirectory.get()}/generated/resources/main/schema.json")
    inputs.dir("${rootDir}/buildSrc/src")
    inputs.file("${rootDir}/buildSrc/build.gradle")
    doLast {
        file("${project.layout.buildDirectory.get()}/generated/sources/rest-codegen").mkdirs()

        new Main().process(
                file("${project.layout.buildDirectory.get()}/generated/resources/main/schema.json"),
                file("${project.layout.buildDirectory.get()}/generated/sources/rest-codegen"),
                "com.github.rahulsom.javacat.rest"
        )
    }
    outputs.dir("${project.layout.buildDirectory.get()}/generated/sources/rest-codegen")
}

tasks.compileJava.dependsOn(generateJava)
tasks.sourcesJar.dependsOn(generateJava)
tasks.javadocJar.dependsOn(generateJava)
tasks.processResources.dependsOn(downloadSchema)
sourceSets.main.java.srcDir("${project.layout.buildDirectory.get()}/generated/sources/rest-codegen")
sourceSets.main.resources.srcDir("${project.layout.buildDirectory.get()}/generated/resources/main")

tasks.withType(Javadoc).configureEach {
    failOnError false
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}
